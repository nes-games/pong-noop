Paddle {
	
	$WIDTH_PX : 4
	$HEIGHT_PX : 2 * Ball.$HEIGHT_PX
	$SPEED : 2
	
	_$SPRITE_1_1_IDX : 2
	_$SPRITE_1_2_IDX : _$SPRITE_1_1_IDX + 1
	_$SPRITE_2_1_IDX : _$SPRITE_1_2_IDX + 1
	_$SPRITE_2_2_IDX : _$SPRITE_2_1_IDX + 1
	_$SPRITE_OFF : 0
	_$SPRITE_1_ON : 2
	_$SPRITE_2_ON : 3
	
	x : 0
	y : 0
	number : 0
	direction : SByte
	
	draw() {
		pal : Pong.$palette
		
		if (number = 1) {
			PPU.$drawSprite(_$SPRITE_1_1_IDX, x, y, _$SPRITE_1_ON, pal)
			PPU.$drawSprite(_$SPRITE_1_2_IDX, x, y + 8, _$SPRITE_1_ON, pal)
		} else {
			PPU.$drawSprite(_$SPRITE_2_1_IDX, x, y, _$SPRITE_2_ON, pal)
			PPU.$drawSprite(_$SPRITE_2_2_IDX, x, y + 8, _$SPRITE_2_ON, pal)
		}
	}
	
	hide() {
		if (number = 1) {
			PPU.$drawSprite(_$SPRITE_1_1_IDX, 0, 0, 0, 0)
			PPU.$drawSprite(_$SPRITE_1_2_IDX, 0, 0, 0, 0)
		} else {
			PPU.$drawSprite(_$SPRITE_2_1_IDX, 0, 0, 0, 0)
			PPU.$drawSprite(_$SPRITE_2_2_IDX, 0, 0, 0, 0)
		}
	}
	
	moveUp() {
		if (y > Playfield.$TOP + $SPEED) {
			y -= $SPEED
			direction := -1
			draw
		}
	}
	
	moveDown() {
		if (y < Playfield.$BOTTOM - $HEIGHT_PX - $SPEED - 1) {
			y += $SPEED
			direction := 1
			draw
		}
	}
	
	moveForCPU(Ball ball) {
		if (ball.speedX > 0 and ball.x > (Playfield.$LEFT + Playfield.$RIGHT) / 3) {
			if (ball.y >= this.y + $HEIGHT_PX) {
				return moveDown
			} else if (ball.y + Ball.$HEIGHT_PX <= this.y) {
				return moveUp
			}
		}

		direction := 0
	}		
}